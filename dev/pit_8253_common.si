# Common logic for PIT 8253 handling
#
# Instead of parametrizing this code to handle both channels 0 and 2, or copy-pasting it twice,
# we keep it in this file that is included twice in pit_8253_ch0.s and pit_8253_ch2.s.

# From the config file
.IMPORT config_log_pit

# From util/error.s
.IMPORT report_error

# From util/bits.s
.IMPORT bit_0
.IMPORT bit_1
.IMPORT bit_2
.IMPORT bit_3
.IMPORT bit_4
.IMPORT bit_5
.IMPORT bit_6
.IMPORT bit_7

# From util/log.s
.IMPORT log_start

# From libxib.a
.IMPORT print_str
.IMPORT print_num
.IMPORT print_num_16_b
.IMPORT print_num_16_w

# Operating modes:
#
# Mode 0:
#  - when mode set: output low, stop
#  - when reload start: output low, stop
#  - when reload done: set value to reload, run (if gate)
#  - when dec from 1 to 0: output high, keep run (if gate)
#
# Mode 1:
#  - when mode set: output high, stop
#  - when reload start: do nothing
#  - when reload done: do nothing
#  - when gate lo->hi: output low, set value to reload, run (if gate)
#  - when dec from 1 to 0: output high, keep run (if gate)
#
# Mode 2:
#  - when mode set: output high, stop
#  - when reload done: if stopped only: set value to reload, run (if gate)
#  - when dec from 2 to 1: output pulse low, set value to reload, keep run (if gate)
#  - when gate hi->lo: output high (which it normally is anyway), stop
#  - when gate lo->hi: set value to reload, run (if gate)
#
# Mode 3, same as mode 2 except:
#  - decrement by 2 instead of 1
#  - mode 2 output change flips a flip-flop, real output comes from the flip-flop
#  - when dec from 2 to 1 -> dec from 2 to 0
#  - when setting value from reload, mask off the 0 bit (not the exact behavior, but close enough)
#    or maybe tweak the condition - when value changes from 2 or 1 (to 0 or -1)
#
# Mode 4:
#  - when mode set: output high, stop
#  - when reload start: stop (not 100% right)
#  - when reload done: set value to reload, run (if gate)
#  - when dec from 1 to 0: output pulse low, keep run (if gate)
#
# Mode 5, same as mode 4 except:
#  - when mode set: output high, stop
#  - when reload start: do nothing
#  - when reload done: do nothing
#  - when dec from 1 to 0: output pulse low, keep run (if gate)
#  - when gate lo->hi: set value to reload, run (if gate)

# The real PIT receives impulses at 14.31818 MHz/12 = 1.1931816666 MHz
# This means there is one PIT pulse every 1/1.1931816666 MHz = every 0.00000084s = every 0.84µs
# Channel 0 is initialized with 0x0000, so it fires IRQ0 every 0x10000 pulses
# This means IRQ0 fires every 0x10000/1.1931816666 MHz = every 0.055s = every 55ms
# There are 1/55ms = 18.2 IRQ0 per second, the BIOS expects IRQ0 18 times per second
#
# The real 8088 executes around 0.33 MIPS at 4.77 MHz
# An average instruction takes 1/0.33 MIPS = 0.000003s = 3µs
# One PIT pulse occurs 3µs/0.84µs times per instruction = 3.6 times per instruction
# One IRQ0 occurs every 55ms/3µs = every 183333 instructions
#
# Actually processing a PIT pulse 3.6 per each instruction would destroy the performance,
# so instead we optimize PIT, trading off timer precision to performance
#
# The emulated PIT receives a pulse every 64 instructions
# This means PIT pulses occur 64 * 3.6 = 230 times less often than on a real IBM PC
# To compensate, we decrement the PIT counters by a 256 times larger value
# In other words, we just decrement the high byte of the counter, ignoring the low byte
# The ratio of timer triggers to number of instructions executed stays rougly the same

# TODO 8253 mode 3 output should come from the flip-flop; it's the same IRQ0 frequency though

##########
pit_vm_callback_common:
.FRAME orig_output, tmp                         # returns continue
    arb -2

    # We are keeping the lo byte of the value untouched, just decrementing the hi byte
    # This is to compensate for triggering this callback less often than the real hardware
    # See design notes at the beginning of this file for more details

    # Is the channel running?
    jz  [pit_running], pit_vm_callback_common_done
    jz  [pit_gate], pit_vm_callback_common_done

    add [pit_output], 0, [rb + orig_output]

    # Decrement the high byte of the value only
    add [pit_value_hi], [pit_decrement], [pit_value_hi]

    lt  [pit_value_hi], 0, [rb + tmp]
    jz  [rb + tmp], pit_vm_callback_common_after_dec

    add [pit_value_hi], 0x100, [pit_value_hi]

pit_vm_callback_common_after_dec:
    # Check if we have reached zero
    jnz [pit_value_hi], pit_vm_callback_common_done

    # Zero reached; set output high
    add 1, 0, [pit_output]

    # Is this mode 2 or 3?
    eq  [pit_mode], 2, [rb + tmp]                           # [0, 1, 2, 3, 4, 5] -> [0, 0, 1, 0, 0, 0]
    add [pit_mode], [rb + tmp], [rb + tmp]                  # [0, 1, 2, 3, 4, 5] + [0, 0, 1, 0, 0, 0] -> [0, 1, 3, 3, 4, 5]
    eq  [rb + tmp], 3, [rb + tmp]
    jz  [rb + tmp], pit_vm_callback_common_after_reload

    # Yes, reload hi byte of the value
    add [pit_reload_hi], 0, [pit_value_hi]

pit_vm_callback_common_after_reload:
    # Is this mode other >=2? If yes, trigger always.
    lt  [pit_mode], 2, [rb + tmp]
    jz  [rb + tmp], pit_vm_callback_common_trigger

    # Mode 0-1, trigger only if output has changed
    eq  [rb + orig_output], [pit_output], [rb + tmp]
    jz  [rb + tmp], pit_vm_callback_common_done

pit_vm_callback_common_trigger:
    call pit_trigger

pit_vm_callback_common_done:
#    # Timer logging
#    # TODO enable this, but only log when the output changes
#    jz  [config_log_pit], pit_vm_callback_common_after_log
#
#    call pit_vm_callback_log
#    call pit_status_log
#
#pit_vm_callback_common_after_log:
    arb 2
    ret 0
.ENDFRAME

##########
pit_mode_command_write_common:
.FRAME value; orig_output, tmp
    arb -2

    # Check for BCD mode, which is not supported
    add bit_0, [rb + value], [ip + 1]
    jnz [0], pit_mode_command_write_common_bcd

    # Read the access mode
    add bit_5, [rb + value], [ip + 1]
    add [0], 0, [rb + tmp]
    mul [rb + tmp], 2, [rb + tmp]
    add bit_4, [rb + value], [ip + 1]
    add [0], [rb + tmp], [rb + tmp]

    # Handle latch command
    jz  [rb + tmp], pit_mode_command_write_common_latch
    add [rb + tmp], 0, [pit_access]

    # Read the operating mode
    add bit_3, [rb + value], [ip + 1]
    add [0], 0, [pit_mode]
    mul [pit_mode], 2, [pit_mode]
    add bit_2, [rb + value], [ip + 1]
    add [0], [pit_mode], [pit_mode]
    mul [pit_mode], 2, [pit_mode]
    add bit_1, [rb + value], [ip + 1]
    add [0], [pit_mode], [pit_mode]

    # Modes 0b110 and 0b111 are aliases for 0b010 and 0b011
    lt  [pit_mode], 0b110, [rb + tmp]
    jnz [rb + tmp], pit_mode_command_write_common_after_aliases
    add [pit_mode], -0b100, [pit_mode]

pit_mode_command_write_common_after_aliases:
    # Reset the channel to its initial state
    add 0, 0, [pit_read_hi_byte_next]
    add 0, 0, [pit_write_hi_byte_next]
    add 0, 0, [pit_running]

    # Decrement by 2 in mode 3, by 1 otherwise
    eq  [pit_mode], 3, [pit_decrement]
    add [pit_decrement], 1, [pit_decrement]
    mul [pit_decrement], -1, [pit_decrement]

    # Save the output, then set output low in mode 0, high otherwise
    add [pit_output], 0, [rb + orig_output]
    lt  0, [pit_mode], [pit_output]

    # Reset the latch
    add -1, 0, [pit_latch_lo]
    add -1, 0, [pit_latch_hi]

    # If output goes hi from lo, trigger
    eq  [pit_output], 0, [rb + tmp]
    add [rb + tmp], [rb + orig_output], [rb + tmp]
    jnz [rb + tmp], pit_mode_command_write_common_log

    call pit_trigger

pit_mode_command_write_common_log:
    # Timer logging
    jz  [config_log_pit], pit_mode_command_write_common_done

    add [rb + value], 0, [rb - 1]
    arb -1
    call pit_mode_command_write_log

    jz  0, pit_mode_command_write_common_done

pit_mode_command_write_common_latch:
    # Don't re-latch an already latched value
    eq  [pit_value_lo], -1, [rb + tmp]
    jnz [rb + tmp], pit_mode_command_write_common_done

    add [pit_value_lo], 0, [pit_latch_lo]
    add [pit_value_hi], 0, [pit_latch_hi]

    # Timer logging
    jz  [config_log_pit], pit_mode_command_write_common_done
    call pit_mode_command_write_latch_log

pit_mode_command_write_common_done:
    arb 2
    ret 1

pit_mode_command_write_common_bcd:
    add pit_mode_command_write_common_bcd_error, 0, [rb - 1]
    arb -1
    call report_error

pit_mode_command_write_common_bcd_error:
    db  "PIT WR: MC Error, BCD mode is not supported", 0
.ENDFRAME

##########
pit_data_write_common:
.FRAME addr, value; position, reload_start, reload_stop, tmp
    arb -4

    # Decide what to write based on access mode
    add pit_data_write_common_table, [pit_access], [ip + 2]
    jz  0, [0]

pit_data_write_common_table:
    db  pit_data_write_common_done
    db  pit_data_write_common_lo
    db  pit_data_write_common_hi
    db  pit_data_write_common_lo_hi

pit_data_write_common_lo:
    # Store lo byte of the value
    add [rb + value], 0, [pit_reload_lo]
    add 0, 0, [pit_reload_hi]

    # We both started and finished a reload
    add 1, 0, [rb + reload_start]
    add 1, 0, [rb + reload_stop]

    jz  0, pit_data_write_common_handle_reload

pit_data_write_common_hi:
    # Store hi byte of the value
    add 0, 0, [pit_reload_lo]
    add [rb + value], 0, [pit_reload_hi]

    # We both started and finished a reload
    add 1, 0, [rb + reload_start]
    add 1, 0, [rb + reload_stop]

    jz  0, pit_data_write_common_handle_reload

pit_data_write_common_lo_hi:
    # Store lo/hi byte of the value
    add pit_reload_w, [pit_write_hi_byte_next], [ip + 3]
    add [rb + value], 0, [0]

    # We wither started and finished a reload, depending on which byte was written
    eq  [pit_write_hi_byte_next], 0, [rb + reload_start]
    eq  [pit_write_hi_byte_next], 1, [rb + reload_stop]

    # Negate pit_write_hi_byte_next
    eq  [pit_write_hi_byte_next], 0, [pit_write_hi_byte_next]

pit_data_write_common_handle_reload:
    # Zero least significant bit of the reload value in mode 3
    eq  [pit_mode], 3, [rb + tmp]
    jz  [rb + tmp], pit_data_write_common_after_mod2
    add bit_0, [pit_reload_lo], [ip + 1]
    jz  [0], pit_data_write_common_after_mod2
    add [pit_reload_lo], -1, [pit_reload_lo]

pit_data_write_common_after_mod2:
    # Did we start a reload?
    jz  [rb + reload_start], pit_data_write_common_after_reload_start

    # Set output low for mode 0
    jnz [pit_mode], pit_data_write_common_after_output_low
    add 0, 0, [pit_output]

pit_data_write_common_after_output_low:
    # Is this mode 0 or 4?
    jz  [pit_mode], pit_data_write_common_stop_timer
    eq  [pit_mode], 4, [rb + tmp]
    jnz [rb + tmp], pit_data_write_common_stop_timer
    jz  0, pit_data_write_common_after_reload_start

pit_data_write_common_stop_timer:
    # Yes, stop the timer
    add 0, 0, [pit_running]

pit_data_write_common_after_reload_start:
    # Did we stop a reload?
    jz  [rb + reload_stop], pit_data_write_common_done

    # Is it mode 0, 4; or stopped in mode 2, 3?
    jz  [pit_mode], pit_data_write_common_update_value
    eq  [pit_mode], 4, [rb + tmp]
    jnz [rb + tmp], pit_data_write_common_update_value

    jnz [pit_running], pit_data_write_common_done

    eq  [pit_mode], 2, [rb + tmp]
    jnz [rb + tmp], pit_data_write_common_update_value
    eq  [pit_mode], 3, [rb + tmp]
    jnz [rb + tmp], pit_data_write_common_update_value

    jz  0, pit_data_write_common_done

pit_data_write_common_update_value:
    # Yes, copy reload to value, start the timer
    add [pit_reload_lo], 0, [pit_value_lo]
    add [pit_reload_hi], 0, [pit_value_hi]
    add 1, 0, [pit_running]

pit_data_write_common_done:
    # Timer logging
    jz  [config_log_pit], pit_data_write_common_after_log

    add [rb + value], 0, [rb - 1]
    arb -1
    call pit_data_write_log

pit_data_write_common_after_log:
    arb 4
    ret 2
.ENDFRAME

##########
pit_data_read_common:
.FRAME addr; value, position, tmp                           # returns value
    arb -3

    # Decide what to read based on access mode
    add pit_data_read_common_table, [pit_access], [ip + 2]
    jz  0, [0]

pit_data_read_common_table:
    db  pit_data_read_common_invalid_mode
    db  pit_data_read_common_lo
    db  pit_data_read_common_hi
    db  pit_data_read_common_lo_hi

pit_data_read_common_invalid_mode:
    # This can only happen before the counter is used the first time
    add 0xff, 0, [rb + value]
    jz  0, pit_data_read_common_clear_latch

pit_data_read_common_lo:
    # Read lo byte from the latch/value
    add [pit_latch_lo], 0, [rb + value]

    eq  [rb + value], -1, [rb + tmp]
    jz  [rb + tmp], pit_data_read_common_clear_latch

    add [pit_value_lo], 0, [rb + value]

    jz  0, pit_data_read_common_done

pit_data_read_common_hi:
    # Read hi byte from the latch/value
    add [pit_latch_hi], 0, [rb + value]

    eq  [rb + value], -1, [rb + tmp]
    jz  [rb + tmp], pit_data_read_common_clear_latch

    add [pit_value_hi], 0, [rb + value]

    jz  0, pit_data_read_common_done

pit_data_read_common_lo_hi:
    # Read lo/hi byte from the latch/value
    add [pit_read_hi_byte_next], 0, [rb + tmp]
    eq  [pit_read_hi_byte_next], 0, [pit_read_hi_byte_next]

    add pit_latch_w, [rb + tmp], [ip + 1]
    add [0], 0, [rb + value]

    eq  [rb + value], -1, [rb + tmp]
    jz  [rb + tmp], pit_data_read_common_clear_latch

    add pit_value_w, [rb + tmp], [ip + 1]
    add [0], 0, [rb + value]

    jz  0, pit_data_read_common_done

pit_data_read_common_clear_latch:
    add -1, 0, [pit_latch_lo]
    add -1, 0, [pit_latch_hi]

pit_data_read_common_done:
    # Timer logging
    jz  [config_log_pit], pit_data_read_common_after_log

    add [rb + value], 0, [rb - 1]
    arb -1
    call pit_data_read_log

pit_data_read_common_after_log:

    arb 3
    ret 1
.ENDFRAME

##########
pit_set_gate_common:
.FRAME value;
    # TODO gate handling
    #
    # mode 1 when gate lo->hi: output low, set value to reload, run (if gate)
    # mode 2 when gate hi->lo: output high (which it normally is anyway), stop
    #        when gate lo->hi: set value to reload, run (if gate)
    # mode 3 same as mode 2 except:
    #        mode 2 output changes a flip-flop, real mode 3 output comes from the flip-flop
    # mode 5 when gate lo->hi: set value to reload, run (if gate)

    add [rb + value], 0, [pit_gate]

    ret 1
.ENDFRAME

##########
pit_vm_callback_log:
.FRAME tmp
    arb -1

    call log_start

    add pit_vm_callback_log_channel, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_channel], [rb + tmp]
    out [rb + tmp]

    add pit_vm_callback_log_value, 0, [rb - 1]
    arb -1
    call print_str

    mul [pit_value_hi], 0x100, [rb - 1]
    add [pit_value_lo], [rb - 1], [rb - 1]
    arb -1
    call print_num_16_w

    out 10

    arb 1
    ret 0

pit_vm_callback_log_channel:
    db  "pit callback: ch", 0
pit_vm_callback_log_value:
    db  ", value 0x", 0
.ENDFRAME

##########
pit_mode_command_write_log:
.FRAME value; tmp
    arb -1

    call log_start

    add pit_mode_command_write_log_channel, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_channel], [rb + tmp]
    out [rb + tmp]

    add pit_mode_command_write_log_value, 0, [rb - 1]
    arb -1
    call print_str

    add [rb + value], 0, [rb - 1]
    arb -1
    call print_num_16_b

    add pit_mode_command_write_log_mode, 0, [rb - 1]
    arb -1
    call print_str

    add [pit_mode], 0, [rb - 1]
    arb -1
    call print_num

    add pit_mode_command_write_log_access, 0, [rb - 1]
    arb -1
    call print_str

    add [pit_access], 0, [rb - 1]
    arb -1
    call print_num

    out 10

    call pit_status_log

    arb 1
    ret 1

pit_mode_command_write_log_channel:
    db  "pit m/c write: ch", 0
pit_mode_command_write_log_value:
    db  ", value 0x", 0
pit_mode_command_write_log_mode:
    db  ", mode ", 0
pit_mode_command_write_log_access:
    db  ", access ", 0
.ENDFRAME

##########
pit_mode_command_write_latch_log:
.FRAME tmp
    arb -1

    call log_start

    add pit_mode_command_write_latch_log_channel, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_channel], [rb + tmp]
    out [rb + tmp]

    add pit_mode_command_write_latch_log_value, 0, [rb - 1]
    arb -1
    call print_str

    mul [pit_value_hi], 0x100, [rb - 1]
    add [pit_value_lo], [rb - 1], [rb - 1]
    arb -1
    call print_num_16_w

    add pit_mode_command_write_latch_log_latch, 0, [rb - 1]
    arb -1
    call print_str

    mul [pit_latch_hi], 0x100, [rb - 1]
    add [pit_latch_lo], [rb - 1], [rb - 1]
    arb -1
    call print_num_16_w

    out 10

    arb 1
    ret 0

pit_mode_command_write_latch_log_channel:
    db  "pit m/c latch: ch", 0
pit_mode_command_write_latch_log_value:
    db  ", value 0x", 0
pit_mode_command_write_latch_log_latch:
    db  ", latch 0x", 0
.ENDFRAME

##########
pit_data_write_log:
.FRAME value; tmp
    arb -1

    call log_start

    add pit_data_write_log_channel, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_channel], [rb + tmp]
    out [rb + tmp]

    add pit_data_write_log_value, 0, [rb - 1]
    arb -1
    call print_str

    add [rb + value], 0, [rb - 1]
    arb -1
    call print_num_16_w

    out 10

    call pit_status_log

    arb 1
    ret 1

pit_data_write_log_channel:
    db  "pit data write: ch", 0
pit_data_write_log_value:
    db  ", value 0x", 0
.ENDFRAME

##########
pit_data_read_log:
.FRAME value; tmp
    arb -1

    call log_start

    add pit_data_read_log_channel, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_channel], [rb + tmp]
    out [rb + tmp]

    add pit_data_read_log_value, 0, [rb - 1]
    arb -1
    call print_str

    add [rb + value], 0, [rb - 1]
    arb -1
    call print_num_16_b

    out 10

    arb 1
    ret 1

pit_data_read_log_channel:
    db  "pit data read: ch", 0
pit_data_read_log_value:
    db  ", value 0x", 0
.ENDFRAME

##########
pit_status_log:
.FRAME tmp
    arb -1

    call log_start

    add pit_status_log_channel, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_channel], [rb + tmp]
    out [rb + tmp]

    add pit_status_log_value, 0, [rb - 1]
    arb -1
    call print_str

    mul [pit_value_hi], 0x100, [rb - 1]
    add [pit_value_lo], [rb - 1], [rb - 1]
    arb -1
    call print_num_16_w

    add pit_status_log_reload, 0, [rb - 1]
    arb -1
    call print_str

    mul [pit_reload_hi], 0x100, [rb - 1]
    add [pit_reload_lo], [rb - 1], [rb - 1]
    arb -1
    call print_num_16_w

    add pit_status_log_latch, 0, [rb - 1]
    arb -1
    call print_str

    eq  [pit_latch_lo], -1, [rb + tmp]
    jnz [rb + tmp], pit_status_log_latch_clear

    out '0'
    out 'x'

    mul [pit_latch_hi], 0x100, [rb - 1]
    add [pit_latch_lo], [rb - 1], [rb - 1]
    arb -1
    call print_num_16_w

    jz  0, pit_status_log_after_latch

pit_status_log_latch_clear:
    add pit_status_log_clear, 0, [rb - 1]
    arb -1
    call print_str

pit_status_log_after_latch:
    add pit_status_log_running, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_running], [rb + tmp]
    out [rb + tmp]

    add pit_status_log_decrement, 0, [rb - 1]
    arb -1
    call print_str

    mul [pit_decrement], -1, [rb + tmp]
    add '0', [rb + tmp], [rb + tmp]
    out [rb + tmp]

    add pit_status_log_gate, 0, [rb - 1]
    arb -1
    call print_str

    add '0', [pit_gate], [rb + tmp]
    out [rb + tmp]

    out 10

    arb 1
    ret 0

pit_status_log_channel:
    db  "pit status: ch", 0
pit_status_log_value:
    db  ", value 0x", 0
pit_status_log_reload:
    db  ", reload 0x", 0
pit_status_log_latch:
    db  ", latch ", 0
pit_status_log_running:
    db  ", running ", 0
pit_status_log_decrement:
    db  ", decrement ", 0
pit_status_log_gate:
    db  ", gate ", 0
pit_status_log_clear:
    db  "(clear)", 0
.ENDFRAME

##########
pit_mode:
    db  0
pit_access:
    db  0

pit_reload_w:
pit_reload_lo:
    db  0
pit_reload_hi:
    db  0

pit_value_w:
pit_value_lo:
    db  0
pit_value_hi:
    db  0

pit_latch_w:
pit_latch_lo:
    db  0
pit_latch_hi:
    db  0

pit_running:
    db  0

# In 16-bit access mode, is the next byte to be accessed hi or low?
pit_read_hi_byte_next:
    db  0
pit_write_hi_byte_next:
    db  0

# This value depends on mode, we precalculate it to speed up vm callback
pit_decrement:
    db  -1

.EOI
